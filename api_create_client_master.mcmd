<command>
  <name>api create client master</name>
  <description>api create client master</description>
  <type>Local Syntax</type>
  <local-syntax>
<![CDATA[
save session variable
 where name = "skip_it"
   and value = 0
|
save session variable
 where name = "err"
   and value = 0
|
publish data
 where client_id = @client_id
   and wh_id = @wh_id
   and adrnam = upper(@adrnam)
   and adrln1 = upper(@adrln1)
   and adrln2 = upper(@adrln2)
   and adrln3 = upper(@adrln3)
   and adrcty = upper(@adrcty)
   and adrpsz = @adrpsz
   and phnnum = @phnnum
   and first_name = upper(@first_name)
   and last_name = upper(@last_name)
   and email_adr = @email_adr
   and web_adr = @web_adr
   and upd_addr = @upd_addr
|
if (@client_id = '')
{
    save session variable
     where name = "skip_it"
       and value = 1
    |
    save session variable
     where name = "errmsg"
       and value = 'Missing client id field value'
    |
    save session variable
     where name = 'err'
       and value = '2005'
}
|
if (@adrnam = '')
{
    save session variable
     where name = "skip_it"
       and value = 1
    |
    save session variable
     where name = "errmsg"
       and value = 'Missing client name field value'
    |
    save session variable
     where name = 'err'
       and value = '2005'
}
|
if (@wh_id = '')
{
    save session variable
     where name = "skip_it"
       and value = 1
    |
    save session variable
     where name = "errmsg"
       and value = 'Missing Warehouse ID field value'
    |
    save session variable
     where name = 'err'
       and value = '2005'
}
|
/* Determine if we should create supplier*/
get session variable
 where name = 'skip_it'
|
publish data
 where skip_it = @value
|
if (@skip_it = 0)
{
    if (@upd_addr = 1)
    {
        [select adr_id @adrid
           from client
          where client_id = @client_id] catch(-942, -1403, 510)
        |
        [update adrmst
            set adrnam = @adrnam,
                adrln1 = @adrln1,
                adrln2 = @adrln2,
                adrln3 = @adrln3,
                adrcty = @adrcty,
                phnnum = @phnnum,
                first_name = @first_name,
                last_name = @last_name,
                email_adr = @email_adr,
                web_adr = @web_adr
          where adr_id = @adrid] catch(-942, -1403, 510)
        |
        save session variable
         where name = 'adr_id'
           and value = @adrid
    }
    else
    {
        [select client_id as check_client
           from client
          where client_id = @client_id] catch(-942, -1403, 510)
        |
        if (@check_client is not null)
        {
            save session variable
             where name = 'err'
               and value = '-1'
            |
            save session variable
             where name = "errmsg"
               and value = 'Client exist, creation failed.' || @?
        }
        else
        {
            generate next number
             where numcod = 'adr_id'
            |
            publish data
             where adr_id = @nxtnum
            |
            create address
             where adr_ids = @adr_id
               and adrnam = @adrnam
               and adrtyp = 'CLI'
               and adrln1 = @adrln1
               and adrln2 = @adrln2
               and adrln3 = @adrln3
               and adrcty = @adrcty
               and phnnum = @phnnum
               and first_name = @first_name
               and last_name = @last_name
               and email_adr = @email_adr
               and web_adr = @web_adr
               and client_id = @client_id
               and rqst_state_cod = 'ACCEPTED' catch(-942, -1403, 510)
            |
            if (@? = 0)
            {
                save session variable
                 where name = 'err'
                   and value = @?
                |
                save session variable
                 where name = "errmsg"
                   and value = 'Cannot create address.' || @?
            }
            |
            create web client
             where client_id = @client_id
               and wh_id = @wh_id
               and adr_id = @adr_id
               and @* catch(-942, -1403, 510)
            |
            if (@? = 0)
            {
                save session variable
                 where name = 'err'
                   and value = @?
                |
                save session variable
                 where name = "errmsg"
                   and value = 'Cannot create client master.' || @?
            }
            |
            save session variable
             where name = 'adr_id'
               and value = @adr_id
        }
    }
}
|
get session variable
 where name = 'err'
|
publish data
 where errid = @value
|
if (@errid != 0)
{
    get session variable
     where name = 'errmsg' catch(@?)
    |
    publish data
     where msg = @value
    |
    if (@msg)
    {
        get mls text
         where mlsid = 'err' || @errid
           and locale_id = @@locale_id catch(@?)
        |
        publish data
         where msg = @mls_text
    }
    |
    publish data
     where data = @client_id
       and message = @msg
}
else
{
    get session variable
     where name = 'adr_id'
    |
    publish data
     where client_id = @client_id
       and adr_id = @value
       and message = 'Client was successfully created!'
}
]]>
</local-syntax>
</command>